load "CharFun.ath"
load "RealExt.ath"

#-------------------------------------------------------------------------
#-- Theories of probability

module Prob {



	#-------------------------  RELEVANT DOMAINS -------------------------  


	#------------------------  RELEVANT FUNCTIONS ------------------------  

	#-- An integral function that computes the real integral of cumulative distribution function of a standard normal distribution by taking the real z-score as input (undefined for now)

	declare integral-SND : [Real] -> Real

	#-- A probabiliity function that takes in a normal distribution Z and a Real T, and returns the probability that a random variable that lies in Z will be at most some real T. It returns zero if the distribution is not normal.   
	#-- If need be, we can overload the probability function according to different input cases

	declare probability : [CharFun.Dist Real]-> Real

	#-- A function that computes the z-score of a value T wrt a normal distribution Z. It returns zero if the distribution is not normal.

	declare z-score : [Real CharFun.Dist] -> Real


	#------------------------  RELEVANT VARIABLES ------------------------  

	define [t T] := [?t:Real ?T:Real]

	define [X Y Z] := [?X:CharFun.Dist ?Y:CharFun.Dist ?Z:CharFun.Dist]
	

	#-------------------------  RELEVANT THEORY  -------------------------	

	assert z-score-axioms :=
	   let{
	        mean := (CharFun.mean Z);
	       stdev := (RealExt.sqrt (CharFun.variance Z));
	      compute := (/ (- T mean) stdev)
	      }
	(fun
	[(z-score T Z) =
	  [ 0.0 when (~(CharFun.is-normal Z))
	    compute when (CharFun.is-normal Z)
	  ]  
	]) 

	define [z-score-Z-not-normal z-score-result] := z-score-axioms


	assert probability-axioms :=
	   let{
	       score := (z-score T Z);
	       integral := (integral-SND score)
	   }
	(fun
	[(probability Z T) =
	   [ 0.0 when (~(CharFun.is-normal Z))
	     integral when (CharFun.is-normal Z)
	   ]
	])  

	define [probability-Z-not-normal probability-result] := probability-axioms


	#-- Probability-Theorem1 states that if two distributions X and Y are normal, then the probability that a random variable that lies in the convolution of X and Y will be at most some real T will be the integral of the SND upto the z score of T wrt the convolution of X and Y

	define Probability-Theorem1 :=
	(
	forall X Y T .  
	(
	    (and (CharFun.is-normal X) (CharFun.is-normal Y) ) ==>
	    (= (probability (CharFun.convolution X Y) T)
	       (integral-SND (z-score T (CharFun.convolution X Y) ) ) )
	)
	)

	conclude Probability-Theorem1
	pick-any x:CharFun.Dist
	pick-any y:CharFun.Dist
	pick-any t:Real
	assume (and (CharFun.is-normal x) (CharFun.is-normal y))
	let{
	    xyNormal := (and (CharFun.is-normal x) (CharFun.is-normal y)); 
		xyCramers := (!uspec (!uspec CharFun.Cramers-Decomposition-Theorem x) y);
		xPlusyNormal := (!mp xyCramers xyNormal);
		txPlusyProbability := (!uspec (!uspec probability-result t) (CharFun.convolution x y) )
	}
	(!mp txPlusyProbability xPlusyNormal) 

} # end module	